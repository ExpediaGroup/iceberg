/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import groovy.transform.Memoized

buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()
    maven { url "http://palantir.bintray.com/releases" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    classpath 'com.palantir.baseline:gradle-baseline-java:0.58.0'
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.14.0'
    classpath 'gradle.plugin.org.inferred:gradle-processors:2.1.0'
    classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.8'
  }
}

plugins {
  id 'com.palantir.git-version' version '0.9.1'
  id 'com.palantir.consistent-versions' version '1.9.2'
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
  throw new GradleException("This build must be run with Java 8")
}

allprojects {
  group = "org.apache.iceberg"
  version = getProjectVersion()
  repositories {
    maven { url  "http://palantir.bintray.com/releases" }
    mavenCentral()
    mavenLocal()
  }
}

subprojects {
  apply plugin: 'java'

  configurations {
    testCompile.extendsFrom compileOnly
    all {
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    testArtifacts
  }

  compileJava {
    options.encoding = "UTF-8"
  }

  compileTestJava {
    options.encoding = "UTF-8"
  }

  ext {
    jmhVersion = '1.21'
  }

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

apply from: 'baseline.gradle'
apply from: 'deploy.gradle'
apply from: 'tasks.gradle'
apply from: 'jmh.gradle'

project(':iceberg-api') {
  dependencies {
    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    testCompile "org.apache.avro:avro"
    testCompile 'joda-time:joda-time'
  }
}

project(':iceberg-common') {

  dependencies {
    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'
  }
}

project(':iceberg-core') {
  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.assemble.dependsOn tasks.shadowJar
  tasks.install.dependsOn tasks.shadowJar
  tasks.javadocJar.dependsOn tasks.shadowJar

  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-common')

    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compile("org.apache.avro:avro") {
      exclude group: 'org.tukaani' // xz compression is not supported
    }

    compile "com.fasterxml.jackson.core:jackson-databind"
    compile "com.fasterxml.jackson.core:jackson-core"
    compile "com.github.ben-manes.caffeine:caffeine"
    compileOnly("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    testCompile project(path: ':iceberg-api', configuration: 'testArtifacts')
  }

  shadowJar {
    // shade compileOnly dependencies to avoid including in transitive dependencies
    configurations = [project.configurations.compile]

    dependencies {
      exclude (dependency('org.apache.avro:avro'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-databind'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-core'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-annotations'))
      exclude (dependency('org.checkerframework:checker-qual'))
      exclude (dependency('com.github.ben-manes.caffeine:caffeine'))
      exclude (dependency('org.slf4j:slf4j-api'))
      exclude (dependency('org.codehaus.mojo:animal-sniffer-annotations'))
      exclude (dependency('org.apache.commons:commons-compress'))
    }

    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'org.apache.calcite', 'org.apache.iceberg.shaded.org.apache.calcite'
  }
}

project(':iceberg-data') {
  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.assemble.dependsOn tasks.shadowJar
  tasks.install.dependsOn tasks.shadowJar
  tasks.javadocJar.dependsOn tasks.shadowJar

  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-core')
    compileOnly project(':iceberg-parquet')
    compileOnly project(':iceberg-orc')

    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compileOnly("org.apache.hadoop:hadoop-common") {
      exclude group: 'commons-beanutils'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    testCompile("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    testCompile project(path: ':iceberg-api', configuration: 'testArtifacts')
  }

  test {
    // Only for TestSplitScan as of Gradle 5.0+
    maxHeapSize '1500m'
  }

  shadowJar {
    // shade compileOnly dependencies to avoid including in transitive dependencies
    configurations = [project.configurations.compile]

    dependencies {
      exclude (dependency('org.apache.avro:avro'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-databind'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-core'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-annotations'))
      exclude (dependency('org.checkerframework:checker-qual'))
      exclude (dependency('com.github.ben-manes.caffeine:caffeine'))
      exclude (dependency('org.slf4j:slf4j-api'))
      exclude (dependency('org.codehaus.mojo:animal-sniffer-annotations'))
      exclude (dependency('org.apache.commons:commons-compress'))
      exclude (dependency('org.apache.iceberg:iceberg-common'))
      exclude (dependency('org.apache.iceberg:iceberg-api'))
      exclude (dependency('org.apache.iceberg:iceberg-core'))
    }

    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'org.apache.calcite', 'org.apache.iceberg.shaded.org.apache.calcite'
  }
}

project(':iceberg-hive') {
  dependencies {
    compile project(':iceberg-core')

    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compileOnly "org.apache.avro:avro"

    compileOnly("org.apache.hive:hive-metastore:2.3.6") {
      exclude group: 'org.apache.hive', module: 'hive-exec'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.apache.hbase'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'co.cask.tephra'
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
      exclude group: 'org.eclipse.jetty.aggregate', module: 'jetty-all'
      exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet'
      exclude group: 'org.apache.parquet', module: 'parquet-hadoop-bundle'
      exclude group: 'com.tdunning', module: 'json'
      exclude group: 'javax.transaction', module: 'transaction-api'
      exclude group: 'com.zaxxer', module: 'HikariCP'
    }

    // By default, hive-exec is a fat/uber jar and it exports a guava library
    // that's really old. We use the core classifier to be able to override our guava
    // version. Luckily, hive-exec seems to work okay so far with this version of guava
    // See: https://github.com/apache/hive/blob/master/ql/pom.xml#L911 for more context.
    testCompile("org.apache.hive:hive-exec:2.3.6:core") {
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.apache.hive', module: 'hive-llap-tez'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.calcite'
      exclude group: 'org.apache.calcite.avatica'
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    testCompile("org.apache.hive:hive-metastore:2.3.6") {
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.apache.hive', module: 'hive-exec'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.apache.hbase'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'co.cask.tephra'
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
      exclude group: 'org.eclipse.jetty.aggregate', module: 'jetty-all'
      exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet'
      exclude group: 'org.apache.parquet', module: 'parquet-hadoop-bundle'
      exclude group: 'com.tdunning', module: 'json'
      exclude group: 'javax.transaction', module: 'transaction-api'
      exclude group: 'com.zaxxer', module: 'HikariCP'
    }

    compileOnly("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    testCompile project(path: ':iceberg-api', configuration: 'testArtifacts')
  }
}

project(':iceberg-mr') {
  dependencies {
    compile project(path: ':iceberg-core', configuration: 'shadow')
    compile project(path: ':iceberg-orc', configuration: 'shadow')
    compile project(path: ':iceberg-parquet', configuration: 'shadow')
    compile project(path: ':iceberg-data', configuration: 'shadow')
	compileOnly "org.apache.avro:avro"
    compileOnly("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    compileOnly('com.github.ben-manes.caffeine:caffeine')
    compileOnly('org.apache.calcite:calcite-core:1.10.0')

    compile("org.apache.hive:hive-serde:2.3.6") {
      exclude group: 'org.apache.parquet', module:'parquet-hadoop-bundle'
      exclude group: 'org.apache.ant', module: '*'
      exclude group: 'javax.servlet', module: 'jsp-api'
      exclude group: 'commons-beanutils', module: 'commons-beanutils-core'
      exclude group: 'javax.annotation', module: '*'
      exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet'
      exclude group: 'org.apache.logging.log4j', module: 'log4j-1.2-api'
      exclude group: 'commons-beanutils', module: 'commons-beanutils'
      exclude group: 'org.apache.geronimo.specs', module: 'geronimo-annotation_1.0_spec'
      exclude group: 'org.checkerframework', module: 'checker-qual'
      exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
      exclude group: 'commons-collections', module: 'commons-collections'
      exclude group: 'org.apache.hive', module: 'hive-exec'
    }
    
    testCompile project(path: ':iceberg-hive', configuration: 'testArtifacts')
    testCompile project(path: ':iceberg-api', configuration: 'testArtifacts')
    testCompile project(path: ':iceberg-core', configuration: 'testArtifacts')
    
    testCompile("org.apache.hive:hive-service:2.3.6") {
      exclude group: 'org.apache.parquet', module:'parquet-hadoop-bundle'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.apache.hive', module: 'hive-exec'
    }
    
    compile("org.apache.hive:hive-exec:2.3.6:core") {
      exclude group: 'stax', module: 'stax-api'
      exclude group: 'commons-collections', module: 'commons-collections'
      exclude group: 'org.apache.ant', module: '*'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.apache.hive', module: 'hive-llap-tez'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.calcite'
      exclude group: 'org.apache.calcite.avatica'
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    
    testCompile 'junit:junit'
    testCompile("com.klarna:hiverunner:4.1.0") {
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.calcite', module: '*'
      exclude group: 'org.codehaus.jettison', module: 'jettison'
      exclude group: 'javax.jms', module: 'jms'
      exclude group: 'org.apache.hive', module: '*'
    }
    testCompile("org.apache.hive:hive-exec:2.3.6:core") {
      exclude group: 'stax', module: 'stax-api'
      exclude group: 'commons-collections', module: 'commons-collections'
      exclude group: 'org.apache.ant', module: '*'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.apache.hive', module: 'hive-llap-tez'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.calcite'
      exclude group: 'org.apache.calcite.avatica'
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
  }
  task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.testCompile
  }
}

project(':iceberg-orc') {
  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.assemble.dependsOn tasks.shadowJar
  tasks.install.dependsOn tasks.shadowJar
  tasks.javadocJar.dependsOn tasks.shadowJar

  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-core')

    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compile("org.apache.orc:orc-core::nohive") {
      exclude group: 'org.apache.hadoop'
      exclude group: 'commons-lang'
      // These artifacts are shaded and included in the orc-core fat jar
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.hive', module: 'hive-storage-api'
    }

    compileOnly("org.apache.hadoop:hadoop-common") {
      exclude group: 'commons-beanutils'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compileOnly("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    testCompile project(path: ':iceberg-api', configuration: 'testArtifacts')
  }

  configurations {
    compile {
      exclude group: 'javax.xml.bind'
    }
  }

  shadowJar {
    // shade compileOnly dependencies to avoid including in transitive dependencies
    configurations = [project.configurations.compile]

    zip64 true

    dependencies {
      exclude (dependency('com.fasterxml.jackson.core:jackson-core'))
      exclude (dependency('org.codehaus.mojo:animal-sniffer-annotations'))
      exclude (dependency('org.apache.iceberg:iceberg-common'))
      exclude (dependency('org.apache.avro:avro'))
      exclude (dependency('org.jetbrains:annotations'))
      exclude (dependency('org.checkerframework:checker-qual'))
      exclude (dependency('io.airlift:aircompressor'))
      exclude (dependency('com.github.ben-manes.caffeine:caffeine'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-annotations'))
      exclude (dependency('org.apache.commons:commons-compress'))
      exclude (dependency('org.apache.orc:orc-shims'))
      exclude (dependency('org.apache.iceberg:iceberg-core'))
      exclude (dependency('org.slf4j:slf4j-api'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-databind'))
      exclude (dependency('org.apache.orc:orc-core'))
      exclude (dependency('org.apache.iceberg:iceberg-api'))

    }

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'org.apache.calcite', 'org.apache.iceberg.shaded.org.apache.calcite'
  }
}

project(':iceberg-parquet') {
  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.assemble.dependsOn tasks.shadowJar
  tasks.install.dependsOn tasks.shadowJar
  tasks.javadocJar.dependsOn tasks.shadowJar

  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-core')

    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compileOnly 'org.slf4j:slf4j-api'
    compileOnly 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compile("org.apache.parquet:parquet-avro") {
      exclude group: 'org.apache.avro', module: 'avro'
      // already shaded by Parquet
      exclude group: 'it.unimi.dsi'
      exclude group: 'org.codehaus.jackson'
    }

    compileOnly "org.apache.avro:avro"
    compileOnly("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    testCompile project(path: ':iceberg-core', configuration: 'testArtifacts')
  }

  configurations {
    compile {
      exclude group: 'javax.xml.bind'
    }
  }

  shadowJar {
    // shade compileOnly dependencies to avoid including in transitive dependencies
    configurations = [project.configurations.compileOnly]

    dependencies {
      exclude (dependency('commons-collections:commons-collections:3.2.2'))
      exclude (dependency('org.apache.hadoop:hadoop-yarn-api'))
      exclude (dependency('org.apache.htrace:htrace-core'))
      exclude (dependency('com.sun.xml.bind:jaxb-impl'))
      exclude (dependency('org.apache.directory.server:apacheds-i18n'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-core'))
      exclude (dependency('xmlenc:xmlenc'))
      exclude (dependency('org.codehaus.jackson:jackson-mapper-asl'))
      exclude (dependency('org.sonatype.sisu.inject:cglib'))
      exclude (dependency('commons-codec:commons-codec'))
      exclude (dependency('org.slf4j:slf4j-api'))
      exclude (dependency('com.sun.jersey:jersey-json'))
      exclude (dependency('org.apache.directory.api:api-util'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-databind'))
      exclude (dependency('org.apache.commons:commons-math3'))
      exclude (dependency('org.codehaus.jackson:jackson-xc'))
      exclude (dependency('org.apache.directory.api:api-asn1-api'))
      exclude (dependency('org.apache.hadoop:hadoop-yarn-common'))
      exclude (dependency('commons-configuration:commons-configuration'))
      exclude (dependency('org.apache.commons:commons-compress'))
      exclude (dependency('javax.xml.bind:jaxb-api'))
      exclude (dependency('org.apache.hadoop:hadoop-auth'))
      exclude (dependency('commons-lang:commons-lang'))
      exclude (dependency('org.apache.curator:curator-client'))
      exclude (dependency('org.apache.hadoop:hadoop-common'))
      exclude (dependency('com.sun.jersey:jersey-client'))
      exclude (dependency('com.sun.jersey:jersey-core'))
      exclude (dependency('javax.servlet:servlet-api'))
      exclude (dependency('org.checkerframework:checker-qual'))
      exclude (dependency('io.netty:netty'))
      exclude (dependency(' org.apache.httpcomponents:httpcore'))
      exclude (dependency('org.apache.avro:avro'))
      exclude (dependency('javax.inject:javax.inject'))
      exclude (dependency('log4j:log4j'))
      exclude (dependency('org.codehaus.jackson:jackson-jaxrs'))
      exclude (dependency('jline:jline'))
      exclude (dependency('org.apache.directory.server:apacheds-kerberos-codec'))
      exclude (dependency('aopalliance:aopalliance'))
      exclude (dependency('asm:asm'))
      exclude (dependency('commons-httpclient:commons-httpclient'))
      exclude (dependency('commons-collections:commons-collections'))
      exclude (dependency('commons-io:commons-io'))
      exclude (dependency('com.fasterxml.jackson.core:jackson-annotations'))
      exclude (dependency('org.apache.hadoop:hadoop-annotations'))
      exclude (dependency('org.apache.zookeeper:zookeeper'))
      exclude (dependency('org.codehaus.jackson:jackson-core-asl'))
      exclude (dependency('org.apache.httpcomponents:httpcore'))
      exclude (dependency('org.codehaus.mojo:animal-sniffer-annotations'))
      exclude (dependency('org.fusesource.leveldbjni:leveldbjni-all'))
      exclude (dependency('com.sun.jersey:jersey-server'))
      exclude (dependency('commons-logging:commons-logging'))
      exclude (dependency('javax.activation:activation'))
      exclude (dependency('org.apache.httpcomponents:httpclient'))
      exclude (dependency('org.mortbay.jetty:jetty-util'))
      exclude (dependency('org.apache.curator:curator-recipes'))
      exclude (dependency('commons-cli:commons-cli'))
      exclude (dependency('com.sun.jersey.contribs:jersey-guice'))
      exclude (dependency('commons-collections:commons-collections'))
      exclude (dependency('com.google.code.findbugs:jsr305'))
      exclude (dependency('commons-digester:commons-digester'))
      exclude (dependency('jline:jline'))
      exclude (dependency('org.codehaus.jettison:jettison'))
      exclude (dependency('org.apache.hadoop:hadoop-yarn-server-common'))
      exclude (dependency('commons-net:commons-net'))
      exclude (dependency('javax.servlet.jsp:jsp-api'))
      exclude (dependency('org.apache.curator:curator-framework'))
    }

    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'org.apache.calcite', 'org.apache.iceberg.shaded.org.apache.calcite'
    relocate 'commons-collections', 'org.apache.iceberg.shaded.commons-collections'
  }
}

project(':iceberg-arrow') {
  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-parquet')

    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compile 'org.slf4j:slf4j-api'
    compile 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compile("org.apache.arrow:arrow-vector") {
      exclude group: 'io.netty', module: 'netty-buffer'
      exclude group: 'io.netty', module: 'netty-common'
    }
    compile("org.apache.arrow:arrow-memory") {
      exclude group: 'io.netty', module: 'netty-common'
    }
  }
}

project(':iceberg-spark') {
  apply plugin: 'scala'

  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-common')
    compile project(':iceberg-core')
    compile project(':iceberg-orc')
    compile project(':iceberg-parquet')
    compile project(':iceberg-arrow')
    compile project(':iceberg-hive')

    compile('com.google.guava:guava') {
      // may be LGPL - use ALv2 findbugs-annotations instead
      exclude group: 'com.google.code.findbugs'
    }
    compile 'org.slf4j:slf4j-api'
    compile 'com.github.stephenc.findbugs:findbugs-annotations:1.3.9-1'

    testCompile 'junit:junit'
    testCompile 'org.slf4j:slf4j-simple'
    testCompile 'org.mockito:mockito-core'

    compileOnly "org.apache.avro:avro"
    compileOnly("org.apache.spark:spark-hive_2.11") {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    testCompile "org.apache.hadoop:hadoop-hdfs::tests"
    testCompile "org.apache.hadoop:hadoop-common::tests"
    testCompile("org.apache.hadoop:hadoop-minicluster") {
      exclude group: 'org.apache.avro', module: 'avro'
    }
    testCompile project(path: ':iceberg-hive', configuration: 'testArtifacts')
    testCompile project(path: ':iceberg-api', configuration: 'testArtifacts')

    // Spark 2.4.4 can only use the below datanucleus version, the versions introduced
    // by Hive 2.3.6 will meet lots of unexpected issues, so here force to use the versions
    // introduced by Hive 1.2.1.
    configurations.all {
      resolutionStrategy {
        force 'org.datanucleus:datanucleus-api-jdo:3.2.6'
        force 'org.datanucleus:datanucleus-core:3.2.10'
        force 'org.datanucleus:datanucleus-rdbms:3.2.9'
      }
    }

    // spark-avro is required only for JMH tests, so ideally we would add this
    // dependency only to the jmh configuration, however gradle-consistent-versions
    // plugin does not respect this configuration and does not seem to have a way
    // to add custom configurations in its lockable configuration detection
    compileOnly("org.apache.spark:spark-avro_2.11") {
      exclude group: 'org.apache.avro', module: 'avro'
    }
  }
}

project(':iceberg-pig') {
  dependencies {
    compile project(':iceberg-api')
    compile project(':iceberg-common')
    compile project(':iceberg-core')
    compile project(':iceberg-parquet')

    compile "org.apache.commons:commons-lang3"

    compileOnly("org.apache.pig:pig") {
      exclude group: "junit", module: "junit"
    }
    compileOnly("org.apache.hadoop:hadoop-mapreduce-client-core")
    compileOnly("org.apache.hadoop:hadoop-client") {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    testCompile "org.apache.hadoop:hadoop-hdfs::tests"
    testCompile "org.apache.hadoop:hadoop-common::tests"
    testCompile("org.apache.hadoop:hadoop-minicluster") {
      exclude group: 'org.apache.avro', module: 'avro'
    }
    testCompile 'junit:junit'
  }
}

// the runtime jar is a self-contained artifact for testing in a notebook
project(':iceberg-spark-runtime') {
  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.assemble.dependsOn tasks.shadowJar
  tasks.install.dependsOn tasks.shadowJar
  tasks.javadocJar.dependsOn tasks.shadowJar

  configurations {
    compileOnly {
      // included in Spark
      exclude group: 'org.slf4j'
      exclude group: 'org.apache.commons'
      exclude group: 'commons-pool'
      exclude group: 'commons-codec'
      exclude group: 'org.xerial.snappy'
      exclude group: 'javax.xml.bind'
    }
  }

  dependencies {
    compileOnly project(':iceberg-spark')
  }

  shadowJar {
    // shade compileOnly dependencies to avoid including in transitive dependencies
    configurations = [project.configurations.compileOnly]

    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'com.fasterxml', 'org.apache.iceberg.shaded.com.fasterxml'
    relocate 'com.github.benmanes', 'org.apache.iceberg.shaded.com.github.benmanes'
    relocate 'org.checkerframework', 'org.apache.iceberg.shaded.org.checkerframework'
    relocate 'org.apache.avro', 'org.apache.iceberg.shaded.org.apache.avro'
    relocate 'avro.shaded', 'org.apache.iceberg.shaded.org.apache.avro.shaded'
    relocate 'com.thoughtworks.paranamer', 'org.apache.iceberg.shaded.com.thoughtworks.paranamer'
    relocate 'org.apache.parquet', 'org.apache.iceberg.shaded.org.apache.parquet'
    relocate 'shaded.parquet', 'org.apache.iceberg.shaded.org.apache.parquet.shaded'
    // relocate Avro's jackson dependency to share parquet-jackson locations
    relocate 'org.codehaus.jackson', 'org.apache.iceberg.shaded.org.apache.parquet.shaded.org.codehaus.jackson'
    relocate 'org.apache.orc', 'org.apache.iceberg.shaded.org.apache.orc'
    relocate 'io.airlift', 'org.apache.iceberg.shaded.io.airlift'
    // relocate Arrow and related deps to shade Iceberg specific version
    relocate 'io.netty.buffer', 'org.apache.iceberg.shaded.io.netty.buffer'
    relocate 'org.apache.arrow', 'org.apache.iceberg.shaded.org.apache.arrow'

    archiveName = "iceberg-spark-runtime-${version}.${extension}"
  }
}

@Memoized
boolean isVersionFileExists() {
  return file('version.txt').exists()
}

@Memoized
String getVersionFromFile() {
  return file('version.txt').text.trim()
}

String getProjectVersion() {
  if (isVersionFileExists()) {
    return getVersionFromFile()
  }

  try {
    return gitVersion()
  } catch (NullPointerException e) {
    throw new Exception("Neither version.txt nor git version exists")
  }
}

String getJavadocVersion() {
  if (isVersionFileExists()) {
    return getVersionFromFile()
  }

  try {
    // use the branch name in place of version in Javadoc
    return versionDetails().branchName
  } catch (NullPointerException e) {
    throw new Exception("Neither version.txt nor git version exists")
  }
}
